buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        springCloudConnectorsVersion = '1.2.1.RELEASE'
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.1.3'
        classpath("io.spring.gradle:dependency-management-plugin:0.5.5.RELEASE")
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    }
}

apply plugin: 'java'

apply plugin: 'eclipse-wtp'
apply plugin: 'idea'

apply plugin: 'spring-boot'
apply plugin: 'cloudfoundry'

apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: "propdeps-idea"

version = '1.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/milestone" }
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:2.0.0.RC1'
    }
}

dependencies {
    // Spring Boot
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-redis"

    // Spring Cloud Connectors
    compile "org.springframework.cloud:spring-cloud-core:${springCloudConnectorsVersion}"
    compile "org.springframework.cloud:spring-cloud-spring-service-connector:${springCloudConnectorsVersion}"
    compile "org.springframework.cloud:spring-cloud-cloudfoundry-connector:${springCloudConnectorsVersion}"

    // JPA Persistence
    compile "commons-dbcp:commons-dbcp"
    runtime "com.h2database:h2"
    runtime "mysql:mysql-connector-java"
    runtime "postgresql:postgresql:9.1-901-1.jdbc4"

    // Redis Persistence
//    compile "redis.clients:jedis"

    // Webjars
    compile "org.webjars:bootstrap:3.1.1"
    compile "org.webjars:angularjs:1.2.16"
    compile "org.webjars:angular-ui:0.4.0-2"
    compile "org.webjars:angular-ui-bootstrap:0.10.0-1"
    compile "org.webjars:jquery:2.1.0-2"

    // Testing
    testCompile "junit:junit"
}

jar {
    baseName = "spring-music"
    version = "" // omit the version from the war file name
}

cloudfoundry {
    target = "https://api.run.pivotal.io"
    space = "development"

    file = file("${jar.archivePath}")
    uri = "spring-music-${randomWord}.cfapps.io"
    memory = 512
    instances = 1
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
